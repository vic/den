{ denTest, ... }:
{
  flake.tests.user-host-mutual-config = {

    test-host-owned-unidirectional = denTest (
      {
        den,
        tuxHm,
        pinguHm,
        ...
      }:
      {
        den.hosts.x86_64-linux.igloo.users = {
          tux = { };
          pingu = { };
        };

        # no mutuality enabled, this is ignored
        den.aspects.igloo.homeManager.programs.direnv.enable = true;

        expr = [
          tuxHm.programs.direnv.enable
          pinguHm.programs.direnv.enable
        ];
        expected = [
          false
          false
        ];
      }
    );

    test-host-owned-mutual = denTest (
      {
        den,
        tuxHm,
        pinguHm,
        ...
      }:
      {
        den.hosts.x86_64-linux.igloo.users = {
          tux = { };
          pingu = { };
        };

        den.ctx.user.includes = [ den._.mutual-provider ];
        den.aspects.igloo._.to-users.homeManager.programs.direnv.enable = true;

        expr = [
          tuxHm.programs.direnv.enable
          pinguHm.programs.direnv.enable
        ];
        expected = [
          true
          true
        ];
      }
    );

    test-host-mutual-static-includes-configures-all-users = denTest (
      {
        den,
        tuxHm,
        pinguHm,
        ...
      }:
      {
        den.hosts.x86_64-linux.igloo.users = {
          tux = { };
          pingu = { };
        };

        den.ctx.user.includes = [ den._.mutual-provider ];

        den.aspects.igloo._.to-users.includes = [
          {
            homeManager.programs.direnv.enable = true;
          }
        ];

        expr = [
          tuxHm.programs.direnv.enable
          pinguHm.programs.direnv.enable
        ];
        expected = [
          true
          true
        ];
      }
    );

    test-host-parametric-unidirectional = denTest (
      {
        den,
        tuxHm,
        pinguHm,
        ...
      }:
      {

        den.hosts.x86_64-linux.igloo.users = {
          tux = { };
          pingu = { };
        };

        den.aspects.igloo.includes = [
          (
            { host, user }:
            {
              homeManager.programs.direnv.enable = true;
            }
          )
        ];

        expr = [
          tuxHm.programs.direnv.enable
          pinguHm.programs.direnv.enable
        ];
        expected = [
          false
          false
        ];
      }
    );

    test-host-parametric-mutual = denTest (
      {
        den,
        tuxHm,
        pinguHm,
        ...
      }:
      {

        den.hosts.x86_64-linux.igloo.users = {
          tux = { };
          pingu = { };
        };

        den.ctx.user.includes = [ den._.mutual-provider ];

        den.aspects.igloo._.to-users.includes = [
          (
            { host, user }:
            {
              homeManager.programs.direnv.enable = true;
            }
          )
        ];

        expr = [
          tuxHm.programs.direnv.enable
          pinguHm.programs.direnv.enable
        ];
        expected = [
          true
          true
        ];
      }
    );

    test-user-owned-configures-all-hosts = denTest (
      {
        den,
        igloo,
        iceberg,
        ...
      }:
      {

        den.hosts.x86_64-linux.igloo.users.tux = { };
        den.hosts.x86_64-linux.iceberg.users.tux = { };

        den.aspects.tux.nixos.programs.fish.enable = true;

        expr = [
          igloo.programs.fish.enable
          iceberg.programs.fish.enable
        ];
        expected = [
          true
          true
        ];
      }
    );

    test-user-static-unidirectional-configures-all-hosts = denTest (
      {
        den,
        igloo,
        iceberg,
        ...
      }:
      {

        den.hosts.x86_64-linux.igloo.users.tux = { };
        den.hosts.x86_64-linux.iceberg.users.tux = { };

        den.aspects.tux.includes = [
          {
            nixos.programs.fish.enable = true;
          }
        ];

        expr = [
          igloo.programs.fish.enable
          iceberg.programs.fish.enable
        ];
        expected = [
          true
          true
        ];
      }
    );

    test-user-function-configures-all-hosts = denTest (
      {
        den,
        igloo,
        iceberg,
        ...
      }:
      {

        den.hosts.x86_64-linux.igloo.users.tux = { };
        den.hosts.x86_64-linux.iceberg.users.tux = { };

        den.aspects.tux.includes = [
          (
            { host, user }:
            {
              nixos.programs.fish.enable = true;
            }
          )
        ];

        expr = [
          igloo.programs.fish.enable
          iceberg.programs.fish.enable
        ];
        expected = [
          true
          true
        ];
      }
    );

    test-user-provides-to-all-users = denTest (
      {
        den,
        lib,
        igloo,
        ...
      }:
      {
        den.ctx.user.includes = [ den._.mutual-provider ];

        den.hosts.x86_64-linux.igloo.users = {
          tux = { };
          alice = { };
          bob = { };
          carl = { };
        };

        den.aspects.tux.provides.to-users =
          { user, ... }:
          {
            homeManager.programs.vim.enable = true;
          };

        den.aspects.tux.provides.alice = {
          homeManager.programs.tmux.enable = true;
        };

        expr = with igloo.home-manager.users; {
          tux = tux.programs.vim.enable;
          alice = alice.programs.vim.enable;
          bob = bob.programs.vim.enable;
          carl = carl.programs.vim.enable;
          aliceTmux = alice.programs.tmux.enable;
          bobTmux = bob.programs.tmux.enable;
        };

        expected = {
          tux = false;
          alice = true;
          bob = true;
          carl = true;
          aliceTmux = true;
          bobTmux = false;
        };

      }
    );

  };
}
